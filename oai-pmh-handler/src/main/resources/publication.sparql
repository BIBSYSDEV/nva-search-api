PREFIX : <https://nva.sikt.no/ontology/publication#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?id ?identifier ?modifiedDate ?title ?date ?type ?publisher (GROUP_CONCAT(?name; separator="|") AS ?contributor) WHERE {
  ?id a :Publication ;
  :identifier ?identifier ;
  :modifiedDate ?modifiedDate ;
  (:|!:)*/:mainTitle ?title ;
  (:|!:)*/:publicationDate ?publicationDate ;
  (:|!:)*/:publicationInstance/rdf:type ?type ;
  (:|!:)*/:publicationContext/rdf:type ?contextType .

  OPTIONAL { ?id (:|!:)*/:contributor/:identity/:name ?name . }

  OPTIONAL { ?id (:|!:)*/:publicationContext/:name ?contextName . }
  OPTIONAL { ?id (:|!:)*/:publicationContext/:publisher/:name ?publisherName . }

  OPTIONAL { ?publicationDate :year ?year . }
  OPTIONAL { ?publicationDate :month ?month . }
  OPTIONAL { ?publicationDate :day ?day . }

  BIND (IF(BOUND(?day) && BOUND(?month) && BOUND(?year), CONCAT(?year, CONCAT("-", CONCAT(?month, CONCAT("-", ?day)))),?year) AS ?date)

  BIND (IF(BOUND(?contextType) && ?contextType = :Journal && BOUND(?contextName), ?contextName, IF(BOUND(?publisherName), ?publisherName, "Unknown")) AS ?publisher)
}
GROUP BY ?id ?identifier ?modifiedDate ?title ?date ?type ?publisher ORDER BY DESC(?identifier) DESC(?modifiedDate)
